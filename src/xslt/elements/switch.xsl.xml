<cm:component xmlns:cm="http://www.agencexml.com/cm" name="switch.xsl">
	<cm:author>Alain Couthures</cm:author>
	<cm:creationdate>2009-04-08</cm:creationdate>
	<cm:lastupdate>2009-04-08</cm:lastupdate>
	<cm:licence>
Copyright (C) 2008-2010 agenceXML - Alain COUTHURES
Contact at : info@agencexml.com

Copyright (C) 2006 AJAXForms S.L.
Contact at: info@ajaxforms.com

This library is free software; you can redistribute it and/or
modify it under the terms of the GNU Lesser General Public
License as published by the Free Software Foundation; either
version 2.1 of the License, or (at your option) any later version.

This library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public
License along with this library; if not, write to the Free Software
Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
	</cm:licence>
	<cm:source type="application/xml">
		<xsl:template match="xforms:switch" xmlns:xhtml="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:xforms="http://www.w3.org/2002/xforms" priority="2">
			<xsl:param name="parentworkid"/>
			<xsl:param name="workid" select="concat(position(),'_',$parentworkid)"/>
			<xsl:variable name="elt">
				<xsl:choose>
					<xsl:when test="namespace-uri(parent::*) = 'http://www.w3.org/2000/svg'">g</xsl:when>
					<xsl:otherwise>div</xsl:otherwise>
				</xsl:choose>
			</xsl:variable>
			<xsl:variable name="ns">
				<xsl:if test="$elt = 'g'">http://www.w3.org/2000/svg</xsl:if>
			</xsl:variable>
			<xsl:element name="{$elt}" namespace="{$ns}">
				<xsl:call-template name="genid">
					<xsl:with-param name="workid" select="$workid"/>
				</xsl:call-template>
				<xsl:call-template name="style">
					<xsl:with-param name="class">xforms-switch</xsl:with-param>
				</xsl:call-template>
				<xsl:variable name="noselected" select="count(xforms:case[@selected='true']) = 0"/>
				<xsl:variable name="caseref" select="@caseref != ''"/>
				<xsl:for-each select="node()">
					<xsl:if test="self::xforms:case">
						<xsl:variable name="nopreviousselected" select="count(preceding-sibling::xforms:case[@selected='true']) = 0"/>
						<xsl:element name="{$elt}" namespace="{$ns}">
							<xsl:call-template name="genid">
								<xsl:with-param name="workid" select="concat(position(),'_',$workid)"/>
							</xsl:call-template>
							<xsl:call-template name="style">
								<xsl:with-param name="class">xforms-case</xsl:with-param>
							</xsl:call-template>
							<xsl:if test="$caseref or not(($noselected and count(preceding-sibling::xforms:case) = 0) or (@selected = 'true' and $nopreviousselected))">
								<xsl:attribute name="style">display:none;</xsl:attribute>
							</xsl:if>
							<xsl:apply-templates select="node()">
								<xsl:with-param name="parentworkid" select="concat(position(),'_',$workid)"/>
							</xsl:apply-templates>
						</xsl:element>
					</xsl:if>
				</xsl:for-each>
			</xsl:element>
		</xsl:template>
	</cm:source>
</cm:component>